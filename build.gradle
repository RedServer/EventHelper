plugins {
	id 'java'
	id 'net.minecraftforge.gradle' version '6.0.+'
}

repositories {
	maven {
		name = 'sponge'
		url = 'https://repo.spongepowered.org/maven'
	}
	maven {
		name = 'spigot'
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
	}
}

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
	mappings channel: 'snapshot', version: '20171003-1.12'
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
	implementation 'org.spongepowered:spongeapi:7.1.0'
	implementation 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

tasks.register('devJar', Jar) {
	archiveClassifier.set('dev')
	from sourceSets.main.output
}

tasks.register('sourcesJar', Jar) {
	archiveClassifier.set('sources')
	from sourceSets.main.allSource
}

artifacts {
	archives tasks.devJar
	archives tasks.sourcesJar
}

jar {
	manifest {
		attributes 'FMLCorePlugin': 'com.gamerforea.eventhelper.coremod.CoreMod'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}
